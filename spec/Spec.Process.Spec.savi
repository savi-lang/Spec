:class Spec.Process.Spec
  :is Spec
  :const describes: "Spec.Process"

  :it "runs a spec that succeeds generates output"
    @ctx.wait_for_action("capture.ok")

    capture = _Effects.Capture.new(_SpecThatSucceeds.Capturer.new(@ctx))
    process = Spec.Process.run_with_capture(capture, [
      Spec.Run(_SpecThatSucceeds).with_capture(@env, capture)
    ])

  :it "runs a spec that fails and generates output"
    @ctx.wait_for_action("capture.fail")

    capture = _Effects.Capture.new(_SpecThatFails.Capturer.new(@ctx))
    process = Spec.Process.run_with_capture(capture, [
      Spec.Run(_SpecThatFails).with_capture(@env, capture)
    ])


:class _SpecThatSucceeds
  :is Spec
  :const describes: "_SpecThatSucceeds"

  :it "succeeds"
    assert: True

:actor _SpecThatSucceeds.Capturer
  :is _Effects.Capture.Notify
  :is Spec.Context.Holder

  :let ctx Spec.Context
  :new (@ctx)

  :be capture_report(err String, out String, exitcode I32)
    assert: exitcode == 0
    assert: out == ""
    assert: err == String.join([
      "_SpecThatSucceeds"
      "  succeeds . \x1b[32mOK\x1b[0m"
      ""
      "test result: \x1b[32mOK\x1b[0m. 1 passed; 0 failed; finished in 0s"
      ""
    ], "\n")

    @ctx.finished_action("capture.ok")

:class _SpecThatFails
  :is Spec
  :const describes: "_SpecThatFails"

  :it "fails"
    assert: False


:actor _SpecThatFails.Capturer
  :is _Effects.Capture.Notify
  :is Spec.Context.Holder

  :let ctx Spec.Context
  :new (@ctx)

  :be capture_report(err String, out String, exitcode I32)
    assert: exitcode == 1
    assert: out == ""
    assert: err == String.join([
      "_SpecThatFails"
      "  fails F \x1b[31mFAIL\x1b[0m"
      ""
      "    FAIL: assert: False"
      ""
      "    Expected to be True"
      ""
      "    # spec/Spec.Process.Spec.savi: 54"
      ""
      "test result: \x1b[31mFAIL\x1b[0m. 0 passed; 1 failed; finished in 0s"
      ""
    ], "\n")

    @ctx.finished_action("capture.fail")

