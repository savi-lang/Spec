:class Spec.Status
  :let examples: Map.Ordered(String, Spec.Status.ForExample).new
  :var expected_examples USize: 0
  :let name String
  :var is_ended Bool: False
  :var is_reported Bool: False
  :new (@name)

:class Spec.Status.ForExample
  :let name String
  :let _runner _Runner.Any
  :let assertions: Array(Spec.Assert).new
  :var assertions_reported USize: 0
  :var is_synchronous_portion_ended Bool: False
  :var is_ended Bool: False
  :var is_reported Bool: False
  :new (@name, @_runner)

  :fun is_waiting_to_end Bool
    !@is_ended && @is_synchronous_portion_ended && @has_any_waiting_actions

  :fun is_ready_to_end Bool
    !@is_ended && @is_synchronous_portion_ended && !@has_any_waiting_actions

  :fun _waiting_action_counts
    counts = Map(String, I32).new
    @assertions.each -> (assertion |
      case assertion <: (
      | Spec.Assert.WaitForAction |
        // TODO: A more streamlined method in Map for doing this "upsert":
        counts[assertion.action] = try (counts[assertion.action]! + 1 | 1)
      | Spec.Assert.FinishedAction |
        // TODO: A more streamlined method in Map for doing this "upsert":
        counts[assertion.action] = try (counts[assertion.action]! - 1 | -1)
      )
    )
    counts

  :fun has_any_waiting_actions
    @_waiting_action_counts.has_any -> (action, count | count > 0)

  :fun each_waiting_action_assertion
    :yields Spec.Assert.WaitForAction for None
    counts = @_waiting_action_counts
    counts.each -> (action, count |
      next if (count <= 0)

      yielded_count = 0
      @assertions.reverse_each -> (assertion |
        if (assertion <: Spec.Assert.WaitForAction) (
          next unless (assertion.action == action)

          yield assertion

          yielded_count += 1
          break if (yielded_count == count)
        )
      )
    )

